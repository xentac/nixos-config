# Vendored from nixpkgs pkgs/by-name/st/stash and bumped to v0.31.x:
# baxter's stash DB is schema 85, which needs stash >= 0.31, while
# nixpkgs (master included) is stuck on 0.29.1 — stash switched the UI
# build from yarn to pnpm in 0.30 and nixpkgs hasn't followed yet, so
# the frontend derivation below is rewritten for pnpm. Drop this whole
# directory (and services.stash.package in media.nix) once nixpkgs'
# stash reaches 0.31+.
{
  buildGoModule,
  fetchFromGitHub,
  lib,
  nixosTests,
  nodejs,
  pnpm_10,
  stash,
  stdenv,
  testers,
}:
let
  inherit (lib.importJSON ./version.json)
    gitHash
    srcHash
    vendorHash
    version
    pnpmHash
    ;

  pname = "stash";
in
buildGoModule (
  finalAttrs:
  let
    frontend = stdenv.mkDerivation (final: {
      pname = "${finalAttrs.pname}-ui";
      inherit (finalAttrs) version gitHash;
      src = "${finalAttrs.src}/ui/v2.5";

      pnpmDeps = pnpm_10.fetchDeps {
        inherit (final) pname version src;
        fetcherVersion = 2;
        hash = finalAttrs.pnpmHash;
      };

      nativeBuildInputs = [
        pnpm_10.configHook
        # Needed for executing package.json scripts
        nodejs
      ];

      postPatch = ''
        substituteInPlace codegen.ts \
          --replace-fail "../../graphql/" "${finalAttrs.src}/graphql/"
        # else pnpm tries to download its own pinned version (offline build)
        sed -i '/"packageManager":/d' package.json
      '';

      buildPhase = ''
        runHook preBuild

        export HOME=$(mktemp -d)
        export VITE_APP_DATE='1970-01-01 00:00:00'
        export VITE_APP_GITHASH=${finalAttrs.gitHash}
        export VITE_APP_STASH_VERSION=v${finalAttrs.version}
        export VITE_APP_NOLEGACY=true

        pnpm run gqlgen
        pnpm run build

        mv build $out

        runHook postBuild
      '';

      dontInstall = true;
      dontFixup = true;
    });
  in
  {
    inherit
      pname
      version
      gitHash
      pnpmHash
      vendorHash
      ;

    src = fetchFromGitHub {
      owner = "stashapp";
      repo = "stash";
      tag = "v${finalAttrs.version}";
      hash = srcHash;
    };

    ldflags = [
      "-s"
      "-w"
      "-X 'github.com/stashapp/stash/internal/build.buildstamp=1970-01-01 00:00:00'"
      "-X 'github.com/stashapp/stash/internal/build.githash=${finalAttrs.gitHash}'"
      "-X 'github.com/stashapp/stash/internal/build.version=v${finalAttrs.version}'"
      "-X 'github.com/stashapp/stash/internal/build.officialBuild=false'"
    ];
    tags = [
      "sqlite_stat4"
      "sqlite_math_functions"
    ];

    subPackages = [ "cmd/stash" ];

    postPatch = ''
      cp -a ${frontend} ui/v2.5/build
    '';

    preBuild = ''
      # `go mod tidy` requires internet access and does nothing
      echo "skip_mod_tidy: true" >> gqlgen.yml
      # remove `-trimpath` fron `GOFLAGS` because `gqlgen` does not work with it.
      # This preBuild is inherited by the goModules fetcher, where vendor/
      # doesn't exist yet (go mod vendor runs after) but the inherited
      # GOFLAGS still say -mod=vendor — drop it there or `go generate`
      # dies with "inconsistent vendoring".
      flags=''${GOFLAGS/-trimpath/}
      if [ ! -d vendor ]; then flags=''${flags/-mod=vendor/}; fi
      GOFLAGS=$flags go generate ./cmd/stash
    '';

    strictDeps = true;

    passthru = {
      inherit frontend;
      updateScript = ./update.py;
      tests = {
        inherit (nixosTests) stash;
        version = testers.testVersion {
          package = stash;
          version = "v${finalAttrs.version} (${finalAttrs.gitHash}) - Unofficial Build - 1970-01-01 00:00:00";
        };
      };
    };

    meta = {
      mainProgram = "stash";
      description = "Organizer for your adult videos/images";
      license = lib.licenses.agpl3Only;
      homepage = "https://stashapp.cc/";
      changelog = "https://github.com/stashapp/stash/blob/v${finalAttrs.version}/ui/v2.5/src/docs/en/Changelog/v${lib.versions.major finalAttrs.version}${lib.versions.minor finalAttrs.version}0.md";
      maintainers = with lib.maintainers; [
        DrakeTDL
      ];
      platforms = [
        "x86_64-linux"
        "aarch64-linux"
        "x86_64-darwin"
        "aarch64-darwin"
      ];
    };
  }
)
